buildscript {
    ext.kotlin_version = '1.0.4'
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'kotlin-android-extensions'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def baseurl = properties.getProperty('baseurl')
def releasePassword = properties.getProperty('releasePassword')
def jwLicenseKey = properties.getProperty('jwLicenseKey')

repositories {
    flatDir {
        dirs 'libs'
    }
    mavenCentral()
}

android {
    compileSdkVersion 24
    buildToolsVersion '22.0.1'

    defaultConfig {
        applicationId "co.touchlab.droidcon.anon"
        minSdkVersion 16
        targetSdkVersion 24
        versionCode 101
        versionName "0.1.01"

        buildConfigField "String", "BASE_URL", "\"$baseurl\""
        resValue "string", "jw_license_key", "$jwLicenseKey"
    }

    productFlavors {
        albany {
            applicationId "co.touchlab.droidcon.anon.albany"
        }
    }

    signingConfigs {
        debug {
            storeFile file("debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
        staging {
            storeFile file("release.jks")
            keyAlias "android"
            storePassword "$releasePassword"
            keyPassword "$releasePassword"
        }
        release {
            storeFile file("release.jks")
            keyAlias "android"
            storePassword "$releasePassword"
            keyPassword "$releasePassword"
        }
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
        }
        staging {
            debuggable true
            minifyEnabled false
            signingConfig signingConfigs.release
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/maven/org.apache.commons/commons-lang3/pom.xml'
        exclude 'META-INF/maven/org.apache.commons/commons-lang3/pom.properties'
        exclude 'jsr305_annotations/Jsr305_annotations.gwt.xml'
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {

    compile 'com.android.support:support-v4:24.2.1'
    compile 'com.squareup.picasso:picasso:2.4.0'
    compile 'com.squareup.okhttp:okhttp:2.4.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.4.0'

    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.github.johnpersano:supertoasts:1.3.4.1@aar'
    compile 'com.nineoldandroids:library:2.4.0'
    compile 'co.touchlab.squeaky:squeaky-query:0.5.0.0'

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'com.android.support:appcompat-v7:24.2.1'
    compile 'com.android.support:design:24.2.1'
    compile 'com.viewpagerindicator:library:2.4.1@aar'
    compile 'com.wnafee:vector-compat:1.0.5'
    compile 'de.hdodenhof:circleimageview:1.3.0'
    compile 'com.android.support:palette-v7:24.2.1'
    compile 'com.android.support:recyclerview-v7:24.2.1'
    compile 'com.android.support:cardview-v7:24.2.1'
    compile 'com.android.support:customtabs:24.2.1'

    compile project(':lib')
    compile project(':jwplayer-android-sdk-2.4.1+158')
}
